// Prefix for globals keys
$__varsPrefix = "zw.webhook:entry"

// Valid services
$__validServices = $lib.set("discord", "slack", "teams", "keybase")

// Fatal error
function __error(mesg) {
    $lib.raise("zw-webhook", $mesg)
}

// Get the full key name for $lib.globals
function __getGlobalsKey(suffix) {
    return(`{$__varsPrefix}:{$suffix}`)
}

// Detect the service for a webhook
// Return the service string if successful, otherwise $lib.null
function __detectWebhookService(url) {
    if ($url.find("slack.com") != $lib.null) {
        return(slack)
    }

    if ($url.find("discord.com") != $lib.null) {
        return(discord)
    }

    if ($url.find("keybase.io") != $lib.null) {
        return(keybase)
    }

    if ($url.find("office") != $lib.null or $url.find("microsoft") != $lib.null) {
        return(teams)
    }

    return($lib.null)
}

// Add the webhook
function addWebhook(name, url, service, global) {
    // Get the service name if not specified
    if ($service = "auto") {
        $service = $__detectWebhookService($url)
        if ($service = $lib.null) {
            $__error(`Failed to detect webhook service for: {$url}`)
        }
    } else {
        if (not $__validServices.has($service)) {
            $__error(`Invalid service specified: {$service}`)
        }
    }

    // Normalize webhook name
    ($ok, $newname) = $lib.trycast(syn:tag:part, $name)
    if (not $ok) {
        $__error(`Failed to normalize webhook name: {$name}`)
    }
    $name = $newname

    $key = $__getGlobalsKey($name)

    // Make sure no webhook exists with that name
    if ($lib.globals.get($key) != $lib.null) {
        $__error(`A webhook already exists with this name: {$name}`)
    }

    // Save the webhook
    $lib.globals.set($key, ({
        "owner": $lib.user.name(),
        "url": $url,
        "service": $service,
        "global": $global
    }))

    $lib.print(`Webhook added for service "{$service}": {$name}`)
}

// Make a Discord-style webhook call
function doDiscordWebhook(url, mesg, verify_ssl) {
    $mesgTrunc = $mesg.slice(0, 2000)
    if ($mesg != $mesgTrunc) {
        $lib.warn("Truncating message to 2000 characters")
    }

    $resp = $lib.inet.http.post($url, json=({"content": $mesgTrunc}), ssl_verify=$verify_ssl)

    if (not ($resp.code >= 200 and $resp.code < 300)) {
        $lib.warn(`Error sending Discord webhook message: {$resp}`)
    }
}

// Make a Slack-style webhook call
function doSlackWebhook(url, mesg, verify_ssl) {
    $resp = $lib.inet.http.post($url, json=({"text": $mesg}), ssl_verify=$verify_ssl)

    if (not ($resp.code >= 200 and $resp.code < 300)) {
        $lib.warn(`Error sending Slack webhook message: {$resp}`)
    }
}

// Make a Teams-style webhook call
function doTeamsWebhook(url, mesg, verify_ssl) {
    $resp = $lib.inet.http.post($url, json=({"text": $mesg}), ssl_verify=$verify_ssl)

    if (not ($resp.code >= 200 and $resp.code < 300)) {
        $lib.warn(`Error sending Teams webhook message: {$resp}`)
    }
}

// Make a Keybase-style webhook call
function doKeybaseWebhook(url, mesg, verify_ssl) {
    $mesgTrunc = $mesg.slice(0, 10000)
    if ($mesg != $mesgTrunc) {
        $lib.warn("Truncating message to 10000 characters")
    }

    $resp = $lib.inet.http.post($url, json=({"msg": $mesgTrunc}), ssl_verify=$verify_ssl)

    if (not ($resp.code >= 200 and $resp.code < 300)) {
        $lib.warn(`Error sending Keybase webhook message: {$resp}`)
    }
}

// Call a webhook
function callWebhook(name, mesg, defang, verify_ssl) {
    $key = $__getGlobalsKey($name)

    // Check if the webhook exists
    $entry = $lib.globals.get($key)
    if ($entry = $lib.null) {
        $__error(`No webhook found with this name: {$name}`)
    }

    // Ensure we own the webhook or anyone can use it
    if ($lib.user.name() != $entry.owner and not $entry.global) {
        $__error(`Not authorized to use this webhook. Owned by: {$entry.owner}`)
    }

    // Defang URLs if requested
    if ($defang) {
        $mesg = $mesg.replace("http", "hxxp").replace(".", "[.]")
    }

    // Make the webhook call
    switch $entry.service {
        "discord": { $doDiscordWebhook($entry.url, $mesg, $verify_ssl) }
        "slack": { $doSlackWebhook($entry.url, $mesg, $verify_ssl) }
        "teams": { $doTeamsWebhook($entry.url, $mesg, $verify_ssl) }
        "keybase": { $doKeybaseWebhook($entry.url, $mesg, $verify_ssl) }
        *: { $__error(`Unsupported webhook service for {$name}: {$entry.service}`) }
    }

    if $lib.debug { $lib.print("Successfully called webhook") }

    return()
}

// Delete a webhook
function deleteWebhook(name) {
    $key = $__getGlobalsKey($name)

    // Check if the webhook exists
    $entry = $lib.globals.get($key)
    if ($entry = $lib.null) {
        $__error(`No webhook found with this name: {$name}`)
    }

    // Check if allowed
    $user = $lib.auth.users.get($lib.user.iden)
    if ($entry.owner != $lib.user.name() and not $user.allowed(zw.webhook.admin)) {
        $__error(`Not allowed to delete webhook "{$name}"! Owned by: {$entry.owner}`)
    }

    $lib.globals.pop($key)
    $lib.print(`Webhook deleted: {$name}`)
}

// List the webhooks
function listWebhooks(show_all) {
    // Check if allowed to list all webhooks
    if $show_all {
        $user = $lib.auth.users.get($lib.user.iden)
        if (not $user.allowed(zw.webhook.admin)) {
            $__error("Not allowed to list all webhooks!")
        }
    }

    // Iterate over all global enties
    for ($key, $value) in $lib.globals.list() {
        // Check if the key is a webhook from this package
        if ($key.startswith($__varsPrefix)) {
            // If we aren't showing all webhooks, make sure we own the webhook
            if (not $show_all and $value.owner != $lib.user.name()) {
                continue
            }

            $name = $key.split(":").index(-1)
            $lib.print(`Webhook: {$name}`)
            $lib.print(`    Owner: {$value.owner}`)
            $lib.print(`    Service: {$value.service}`)
            $lib.print(`    Global: {$value.global}`)
            $lib.print(`    URL: {$value.url}`)
            $lib.print("")
        }
    }
}